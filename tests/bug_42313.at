# bug_42313.at -- bug 42313   -*- Autotest -*-

#   Copyright (C) 2024 Free Software Foundation, Inc.
#
#   This file is part of GNU Libtool.
#
# GNU Libtool is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License as
# published by the Free Software Foundation; either version 2 of
# the License, or (at your option) any later version.
#
# GNU Libtool is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with GNU Libtool.  If not, see <https://www.gnu.org/licenses/>.
####

AT_BANNER([Testing bug 42313:])

AT_SETUP([verify no autoscan AC_PROG_RANLIB warning])

AT_DATA([configure.ac],
[[
AC_PREREQ([2.69])
AC_INIT([testcase], ]AT_PACKAGE_VERSION[, ]AT_PACKAGE_BUGREPORT[)
LT_INIT
AC_CONFIG_MACRO_DIR([m4])
AC_OUTPUT
]])

LT_AT_LIBTOOLIZE([--install])
LT_AT_AUTORECONF([--install])
AT_CHECK([autoscan || exit 77], [0], [stdout], [stderr])
AT_CHECK([$GREP -- 'AC_PROG_RANLIB' stderr], [1], [ignore])

AT_CLEANUP

AT_SETUP([check AC_PROG_RANLIB declaration has warning])

AT_DATA([configure.ac],
[[
AC_PREREQ([2.69])
AC_INIT([testcase], ]AT_PACKAGE_VERSION[, ]AT_PACKAGE_BUGREPORT[)
LT_INIT
AC_CONFIG_MACRO_DIR([m4])
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_OUTPUT
]])

LT_AT_LIBTOOLIZE([--install])
LT_AT_AUTORECONF([--install])
AT_CHECK([$GREP -- 'obsolete' stdout], [0], [ignore])

AT_CLEANUP
